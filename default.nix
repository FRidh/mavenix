{
  mavenix ? import ./mavenix.nix {},
  pkgs ? mavenix.pkgs,
}: with pkgs;

let
  inherit (mavenix) name version;
  gen-header = "# This file has been generated by ${name} ${version}.";

  default-tmpl = writeText "default-tmpl.nix" ''
    ${gen-header} Configure the build here!
    {
      mavenix ? import ./%%env%% {},
      pkgs ? mavenix.pkgs,
      src ? ./%%src%%,
      doCheck ? false,
    }: mavenix.buildMaven {
      inherit src doCheck;
      infoFile = toString ./%%info%%;

      # Add build dependencies
      #   buildInputs = with pkgs; [ git ];

      # Add extra maven dependencies
      #   deps = [ { path = "org/group-id/artifactId/version/file"; sha1 = "0123456789abcdef"; } ];

      # Add dependencies on other mavenix derivations
      #   drvs = [ (import ../other/mavenix/derivation {}) ];

      # Override which maven package to build with
      #   maven = maven.override { jdk = pkgs.oraclejdk10; };

      # remotes = { central = "https://repo.maven.apache.org/maven2"; };
      # settings = ./settings.xml;
    }
  '';
in stdenv.mkDerivation {
  inherit name;
  src = ./.;
  buildInputs = [ makeWrapper ];
  installPhase = ''
    mkdir -p $out/bin
    cp mvnix-init mvnix-update $out/bin
    wrapProgram $out/bin/mvnix-init \
      --set CONFIG_TEMPLATE ${default-tmpl} \
      --set MAVENIX_SCRIPT  ${./mavenix.nix}
    wrapProgram $out/bin/mvnix-update \
      --prefix PATH : ${lib.makeBinPath [ yq nix mktemp ]}
  '';
  meta = with stdenv.lib; {
    homepage = https://github.com/icetan/mavenix;
    description = "Mavenix: deterministic builds for Maven using Nix?";
    license = licenses.unlicense;
    maintainers = [ { email = "me@icetan.org"; github = "icetan"; name = "Christopher Fred√©n"; } ];
    platforms = [ "x86_64-linux" ];
  };
}
